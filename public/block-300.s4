0(-Editor-)
1xBL
10 xBL

100 sXS 20 sYS rXS rYS* sBSZ
rV sBS rV rBSZ+ 1- sBE
"start:" rBS. ", end"rBE.
:EDIT sBN 1sGO RBLK CLS rGO{\ COF DISP FOOTER CON xK@ #,sC EDITCH rGO};
:CBLK rBS rBE[rI c@ 32<(10 rI c!)];
:RBLK rBS rBE[0rIc!] rBN rBS rBSZ xBR \ rV sU CBLK;
:WBLK rBN rBS rBSZ xBW \ rV sU;
:RULE " +" 1 rXS ["-"] "+" CR;
:HDR 1# GXY " Editor v0.1 Block: " rBLK. CR;
:FA "  (a)left (d)right (w)up (s)down (q)<< (e)>> (t)top (l)last" CR;
:FC "  (n)LF (W)Write (Q)Quit" CR;
:FB "  (r)replace (R)Replace (i)insert (I)Insert" CR;
:FOOTER FA FB FC ": "8,;
:PC #10=(\250),;
:DISP HDR rBS s1 RULE 1 rYS[" |"1 rXS[r1 rU=(178,) r1 c@ PC i1]"| "CR] RULE;
:NORM s1r1rV<(rV s1) r1rBE>(rBE s1) r1;
:INS rBE #s2 s1d1 r2rU>{\r1c@r2c! d1d2 r2rU>}32rUc!;
:TYPEI 1{\xK@ #s1 27=(|) INS r1rUc! iU DISP COF 1};
:TYPER 1{\xK@ #s1 27=(|) r1rUc! iU DISP COF 1};
:DEL rU rBE[rI1+c@ rIc!]10rBEc!;
:CHA rC'd=(iU;) rC'a=(dU;) rC'w=(rUrXS-sU;) rC's=(rUrXS+sU);
:CHB rC'q=(rU4-sU;) rC'e=(rU4+sU;) rC't=(0sU;) rC'l=(rU9999+sU);
:CHC rC'r=(xK@rUc!iU;) rC'n=(10rUc!iU;) rC'x=(DEL;) rC'i=(INS;);
:CHD rC'W=(WBLK;) rC'I=(TYPEI;) rC'R=(COF TYPER CON;);
:EDITCH rC'Q=(0 sGO;) CHA CHB CHC CHD rU NORM sU;

:RL 300 xBL;
300 EDIT






































































































































































































































































































































































































































































































































































































































































































































































































































